/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group 'NNA'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.4'
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.13.0"
    }
}

apply plugin: 'idea'
apply plugin: 'os-package'
apply plugin: 'license'
apply plugin: 'com.diffplug.gradle.spotless'

ext {
    hadoopVersion = (properties.get("hadoopVersion") == null) ? "2.7.0" : "${hadoopVersion}"
    println("Building with hadoopVersion: ${hadoopVersion}")
}

checkstyle {
    toolVersion "7.8.2"
    configFile file("config/checkstyle/checkstyle.xml")
}

spotless {
    java {
        googleJavaFormat()
        licenseHeaderFile file('license.header.spotless'), '(package|import)'
    }
}

license {
    header = file('license.header')
    excludePatterns = [ "**/bootstrap*",
                        "**/Chart*",
                        "**/cycle.js",
                        "**/jquery*",
                        "**/sweetalert*",
                        "**/npm.js",
                        "**/*.blank.xml",
                        "**/*.md" ].toSet()
}

findbugs {
    findbugsTest.enabled = false
}

jacoco {
    toolVersion = "0.8.1"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport 
}

sourceSets.main.java.srcDirs "src/main/java", "src/main/${hadoopVersion}"
sourceSets.test.java.srcDirs "src/test/java", "src/test/${hadoopVersion}"

repositories {
    mavenCentral()
    maven {
        url 'http://repo.hortonworks.com/content/repositories/releases/'
    }
}

configurations {
    compile.extendsFrom(outboundDep)
    all*.exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    all*.exclude group: 'javax.servlet', module: 'servlet-api'
}

dependencies {
    outboundDep group: 'org.pac4j', name: 'spark-pac4j', version: '1.2.3'
    outboundDep group: 'org.pac4j', name: 'pac4j-ldap', version: '1.9.4'
    outboundDep group: 'org.pac4j', name: 'pac4j-jwt', version: '1.9.4'
    outboundDep group: 'com.sparkjava', name: 'spark-core', version: '2.5'
    outboundDep group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    outboundDep group: 'org.mortbay.jetty', name: 'jetty', version: '6.1.26'
    outboundDep group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    outboundDep group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    outboundDep group: 'log4j', name: 'log4j', version: '1.2.17'
    outboundDep group: 'javax.mail', name: 'mail', version: '1.4.7'
    outboundDep group: 'org.hsqldb', name: 'hsqldb', version: '2.4.0'
    outboundDep group: 'org.mapdb', name: 'mapdb', version: '3.0.5'
    outboundDep(group: 'com.google.guava', name: 'guava', version: '11.0.2') {
        force = true
    }
    outboundDep group: 'org.apache.hadoop', name: 'hadoop-common', version: "${hadoopVersion}"
    outboundDep group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: "${hadoopVersion}"
    testCompile group: 'org.apache.hadoop', name: 'hadoop-common', version: "${hadoopVersion}", classifier: 'tests'
    testCompile group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: "${hadoopVersion}", classifier: 'tests'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.jmockit', name: 'jmockit', version: '1.22'

    if (hadoopVersion.startsWith("3.")) {
        outboundDep group: 'org.apache.hadoop', name: 'hadoop-hdfs-client', version: "${hadoopVersion}"
    }
}

ospackage {
    packageName = 'nn-analytics'
    version = "1.2_${hadoopVersion}"
    release = '250'
    arch = NOARCH
    os = LINUX

    user 'root'
    permissionGroup 'root'

    postInstall 'mkdir -p -m 0755 /usr/local/nn-analytics/dfs/name/current && ' +
            'mkdir -p -m 0755 /usr/local/nn-analytics/dfs/name/legacy && ' +
            'chown -R hdfs: /usr/local/nn-analytics/dfs &&' +
            'mkdir -p -m 0755 /usr/local/nn-analytics/db && ' +
            'chown -R hdfs: /usr/local/nn-analytics/db &&' +
            'mkdir -p -m 0755 /var/log/nn-analytics && ' +
            'chown hdfs: /var/log/nn-analytics &&' +
            'cp /usr/local/nn-analytics/bin/nn-analytics /etc/init.d/ &&' +
            'chkconfig --add nn-analytics'

    into '/usr/local/nn-analytics/'

    from(jar.outputs.files) {
        into 'lib'
    }
    from(configurations.outboundDep.files) {
        into 'lib'
    }
    from('src/main/resources/bin') {
        into 'bin'
        fileMode = 0755
    }
    from('src/main/resources/config') {
        fileType CONFIG | NOREPLACE
        into 'config'
    }

}
